plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace = 'net.artemkv.journey3'

    compileSdk 32

    defaultConfig {
        minSdk 26
        minSdkVersion 26
        targetSdk 32

        aarMetadata {
            minCompileSdk = 26
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'net.artemkv'
            artifactId = 'journey3'
            version = '1.2.0'

            afterEvaluate {
                from components.release
            }

            pom {
                name = 'net.artemkv:journey3'
                description = 'Android client for Journey3 (Lightweight Anonymous Mobile Analytics)'
                url = 'https://journey3.net/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'artemkv'
                        name = 'Artem Kondratyev'
                        email = 'artemkv@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/artemkv/journey3-android-sdk.git'
                    developerConnection = 'scm:git:ssh://github.com/artemkv/journey3-android-sdk.git'
                    url = 'http://github.com/artemkv/journey3-android-sdk/tree/master'
                }
            }
        }
    }
    repositories {
        maven {
            // url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = mavenUsername
                password = mavenPassword
            }
        }
    }
}

signing {
    sign publishing.publications.release
}

dependencies {
    implementation 'com.google.code.gson:gson:2.9.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}